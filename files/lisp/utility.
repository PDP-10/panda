(FILECREATED "18-Aug-84 14:41:07" <NEWLISP>UTILITY..155 36628  

      changes to:  (VARS UTILITYCOMS)

      previous date: "10-NOV-82 23:29:35" <NEWLISP>UTILITY..154)


(PRETTYCOMPRINT UTILITYCOMS)

(RPAQQ UTILITYCOMS ((COMS * DIRCOMS)
		    (COMS (* Read macros for * and ^W)
			  (FNS CONTROLW READLINEP READCOMMENT GETCOMMENT PRINTCOMMENT)
			  [VARS (NORMALCOMMENTSFLG (COND ((NOT (BOUNDP (QUOTE NORMALCOMMENTSFLG)))
							  T)
							 (T NORMALCOMMENTSFLG]
			  (P (PROGN (SETSYNTAX (CHARCODE ^W)
					       (QUOTE (INFIX IMMEDIATE CONTROLW))
					       T)
				    (SETSYNTAX (CHARCODE ^W)
					       T EDITRDTBL)
				    (ECHOCONTROL (CHARCODE ^W)
						 (QUOTE IGNORE)))
			     (SETSYNTAX (QUOTE *)
					(QUOTE (INFIX ALONE NOESC READCOMMENT))
					FILERDTBL))
			  (ADDVARS (PRETTYPRINTMACROS (* . PRINTCOMMENT)))
			  (USERMACROS GET*)
			  (DECLARE: DOEVAL@COMPILE DONTCOPY (RECORDS COMMENTBOX)))
		    (COMS (* useful under DIRECTORY)
			  (FNS SYSOUTP READASCIZ))
		    (COMS (* ASSEMBLETRAN)
			  (FNS ASSEMBLETRAN)
			  (PROP CLISPWORD ASSEMBLE))
		    (FNS HELPBINSEARCH)
		    (DECLARE: DOEVAL@COMPILE DONTCOPY (PROP MACRO MPWRD))))

(RPAQQ DIRCOMS ((DECLARE: FIRST (ADDVARS (NOSWAPFNS UNDELFILE)))
		(FNS DODIR DIRECTORY DODIRECTORY DOCOMMANDS CHECKPATTERN FILENAME DIR2 DIR4 DREAD DPRIN1 DTAB NEXTFILE DIRPATTERN 
		     DIRPRINTNAME DMATCH DPAT)
		(FNS /UNDELFILE /DELFILE UNDELFILE)
		(P (NEW/FN (QUOTE DELFILE))
		   (NEW/FN (QUOTE UNDELFILE)))
		(VARS DIRCOMMANDS FILEINFOTYPES)
		(LISPXMACROS DIR)
		(DECLARE: EVAL@COMPILE DONTCOPY (RECORDS FILEGROUP)
			  (FILES (SYSLOAD FROM LISPUSERS)
				 CJSYS)
			  (MACROS STRPOSL))
		(ALISTS (JSYSES GNJFN))
		(BLOCKS (NIL DIRECTORY DODIR /UNDELFILE /DELFILE UNDELFILE (LOCALVARS . T))
			(DODIRECTORY DODIRECTORY CHECKPATTERN DOCOMMANDS DIR2 DIR4 DREAD DPRIN1 DTAB NEXTFILE DIRPATTERN 
				     DIRPRINTNAME DMATCH DPAT (LOCALFREEVARS COLUMNS VALUE I JFN FILEGROUP NAMEPRINTED SCRATCHSTR 
									     GTJFNFLAGS J NAMEFLG FILENAME FILE)
				     (SPECVARS DEFAULTVERS DEFAULTEXT)
				     (GLOBALVARS DIRCOMMANDS ASKUSERTTBL CHCONLST2 FILEINFOTYPES RESETVARSLST)
				     FILENAME))))
(DECLARE: FIRST 

(ADDTOVAR NOSWAPFNS UNDELFILE)
)
(DEFINEQ

(DODIR
  [LAMBDA (LISPXLINE EXTRACOMS DEFAULTEXT DEFAULTVERS NOP)
                                   (* lmm "22-APR-81 23:28")
    (PROG ((FILE (CAR LISPXLINE))
	   (TAIL (CDR LISPXLINE)))
      LP  (COND
	    ((FMEMB (CAR TAIL)
		    (QUOTE (OR + * AND - ANDNOT)))
	      (SETQ FILE (LIST FILE (CAR TAIL)
			       (CADR TAIL)))
	      (SETQ TAIL (CDDR TAIL))
	      (GO LP)))
          (AND EXTRACOMS (SETQ TAIL (APPEND TAIL EXTRACOMS)))
          (OR NOP (FMEMB (QUOTE P)
			 TAIL)
	      (FMEMB (QUOTE PP)
		     TAIL)
	      (SETQ TAIL (CONS (QUOTE P)
			       TAIL)))
          (RETURN (DIRECTORY FILE TAIL DEFAULTEXT DEFAULTVERS])

(DIRECTORY
  [LAMBDA (FILES COMMANDS DEFAULTEXT DEFAULTVERS)
    (DECLARE (SPECVARS FILES COMMANDS DEFAULTEXT DEFAULTVERS))
                                   (* lmm "22-APR-81 23:29")
    (DODIRECTORY])

(DODIRECTORY
  [LAMBDA NIL                      (* lmm "22-APR-81 23:29")
    (RESETLST (PROG (VALUE COLUMNS NAMEFLG DELETEDONLY FILEGROUP (SCRATCHSTR (CONSTANT (CONCAT)))
			   PRINTFLG OUTFILE PROMPTFLG FILE)
		    (PROG [(X (SETQ COMMANDS (COND
				  ((LISTP COMMANDS)
				    (APPEND COMMANDS))
				  (T (SETQ COMMANDS (LIST (OR COMMANDS (QUOTE COLLECT]
		      COMLP
		          [SELECTQ (CAR X)
				   ((PAUSE P PP AUTHOR)
				     (SETQ PRINTFLG T))
				   (OLDVERSIONS [OR (FIXP (CADR X))
						    (RPLACD X (CONS 1 (CDR X]
						(pop X))
				   (TRIMTO (SETQ PRINTFLG T)
					   [OR (FIXP (CADR X))
					       (RPLACD X (CONS 1 (CDR X]
					   (pop X))
				   ((DELETE COLLECT))
				   (COLUMNS [SETQ COLUMNS (CAR (SETQ X (CDR X]
					    (SETQ PRINTFLG T))
				   (COUNTSIZE (SETQ VALUE 0))
				   (PROMPT (SETQ X (CDR X))
					   (SETQ PROMPTFLG T))
				   (PRINT (pop X)
					  (SETQ PRINTFLG T))
				   (@ (SETQ X (CDR X))
				      [COND
					((FNTYP (CAR X))
					  (RPLACA X (CONS (CAR X)
							  (SELECTQ (NARGS (CAR X))
								   (1 (QUOTE (JFN)))
								   (QUOTE (JFN FILENAME]
				      (AND (FMEMB (QUOTE FILENAME)
						  (FREEVARS (CAR X)))
					   (SETQ NAMEFLG T)))
				   [OUT (SETQ OUTFILE (CAR (SETQ X (CDR X]
				   ((DELETED UNDELETE)
				     (SETQ DELETEDONLY T))
				   [OLDERTHAN (RPLACA (SETQ X (CDR X))
						      (IDIFFERENCE (IDATE)
								   (ITIMES
								     (CAR X)
								     (CONSTANT (IDIFFERENCE
										 (IDATE 
										 "2-JAN-77 00:00")
										 (IDATE 
										 "1-JAN-77 00:00"]
				   [BY (RPLACA (SETQ X (CDR X))
					       (USERNUMBER (CAR X]
				   (COND
				     ((STRINGP (CAR X))
				       (SETQ PRINTFLG T))
				     ((FASSOC (CAR X)
					      FILEINFOTYPES)
				       (SETQ PRINTFLG T))
				     ((LISTP (CAR X))
				       (FRPLNODE2 X (APPEND (CAR X)
							    (CDR X)))
				       (GO COMLP))
				     ((FIXSPELL (CAR X)
						NIL
						(NCONC (MAPCAR FILEINFOTYPES (FUNCTION CAR))
						       DIRCOMMANDS)
						NIL X NIL NIL T)
				       (GO COMLP))
				     (T (ERROR "invalid DIR command" (CAR X]
		          (AND (SETQ X (CDR X))
			       (GO COMLP)))
		    (OR (SETQ FILEGROUP (DIRPATTERN FILES DEFAULTEXT (OR (FIXP DEFAULTVERS)
									 (SELECTQ DEFAULTVERS
										  ((NIL *)
										    -3)
										  0))
						    DELETEDONLY))
			(PROGN (COND
				 (PRINTFLG (printout T "no such files" T)))
			       (RETURN VALUE)))
		    (COND
		      ((OR PRINTFLG OUTFILE PROMPTFLG)
			[COND
			  (PROMPTFLG (RESETSAVE (SETTERMTABLE ASKUSERTTBL]
			(RESETSAVE (OUTPUT T))
			[COND
			  (OUTFILE (COND
				     ((OPENP OUTFILE)
				       (OUTPUT OUTFILE))
				     (T (OUTFILE OUTFILE)
					(RESETSAVE NIL (LIST (QUOTE CLOSEF)
							     (OUTPUT]
			(SETQ PRINTFLG T)
			(TAB 0 0)))
		FILELP
		    (DOCOMMANDS COMMANDS)
		    (COND
		      ((SETQ FILEGROUP (NEXTFILE FILEGROUP))
			(GO FILELP)))
		    (COND
		      (PRINTFLG (TAB 0 0)))
		    (RETURN VALUE])

(DOCOMMANDS
  (LAMBDA (COMMANDS)                                        (* lmm "15-DEC-78 09:34")
    (PROG ((JFN (fetch JFN of FILEGROUP))
	   (Y COMMANDS)
	   (I 0)
	   FILENAME NAMEPRINTED TEM TEM2)
          (AND COLUMNS (COND
		 ((NOT (ILESSP (SETQ I (ITIMES (IQUOTIENT (IPLUS (POSITION)
								 COLUMNS -1)
							  COLUMNS)
					       COLUMNS))
			       (IDIFFERENCE (LINELENGTH)
					    30)))
		   (SETQ I 0))))
      DOCOM
          (COND
	    (Y (SELECTQ (CAR Y)
			(P (DIRPRINTNAME))
			(PP (DIRPRINTNAME T))
			(COUNTSIZE (SETQ VALUE (IPLUS (JS SIZEF JFN NIL 0 3)
						      VALUE)))
			(PAUSE (READC T)
			       (SETQ I (IPLUS I 2)))
			(@                                  (* Arbitrary predicate -- next thing is form)
			   (AND NAMEFLG (FILENAME))
			   (COND
			     ((NOT (EVAL (CAR (SETQ Y (CDR Y)))))
			       (RETURN))))
			(OLDVERSIONS (OR (IGEQ (fetch OLDVER of FILEGROUP)
					       (CAR (SETQ Y (CDR Y))))
					 (RETURN)))
			(OLDERTHAN (COND
				     ((OR (IGEQ (GETFILEINFO JFN (QUOTE IREADDATE))
						(CAR (SETQ Y (CDR Y))))
					  (IGEQ (GETFILEINFO JFN (QUOTE IWRITEDATE))
						(CAR Y)))
				       (RETURN))))
			(BY (OR (EQUAL (GETFILEINFO JFN (QUOTE IAUTHOR))
				       (CAR (SETQ Y (CDR Y))))
				(RETURN)))
			(DELETE (DTAB 12)
				(PRIN1 (COND
					 ((/DELFILE (FILENAME))
					   "deleted")
					 (T "can't delete"))))
			(PROMPT (OR (DREAD (CAR (SETQ Y (CDR Y))))
				    (RETURN)))
			(PRINT (DPRIN1 (CAR Y)))
			(COLLECT (SETQ VALUE (CONS (FILENAME)
						   VALUE)))
			((OUT COLUMNS)
			  (pop Y))
			(DELETED)
			(UNDELETE (DTAB 14)
				  (PRIN1 (COND
					   ((/UNDELFILE (FILENAME))
					     "undeleted")
					   (T "can't undelete"))))
			(TRIMTO (SETQ TEM (JS DELNF JFN (CAR (SETQ Y (CDR Y)))
					      NIL 2))
				(COND
				  ((EQ (SYSTEMTYPE)
				       (QUOTE TOPS20))
				    (SETQ TEM (IMINUS TEM))))
				(COND
				  ((MINUSP TEM)
				    (DIRPRINTNAME T)
				    (DTAB 4)
				    (PRINTNUM (QUOTE (FIX 4))
					      (IMINUS TEM))
				    (PRIN1 (COND
					     ((EQ TEM -1)
					       " version  deleted")
					     (T " versions deleted")))
				    (add I 20))))
			(COND
			  ((STRINGP (CAR Y))
			    (PRIN1 (CAR Y))
			    (add I (NCHARS (CAR Y))))
			  ((SETQ TEM (FASSOC (CAR Y)
					     FILEINFOTYPES))
			    (DTAB (CADR TEM))
			    (COND
			      ((SETQ TEM2 (GETFILEINFO JFN (CAR Y)
						       (CONSTANT (CONCAT))))
				(COND
				  ((FIXP TEM2)
				    (PRINTNUM (OR (CDDR TEM)
						  (LIST (QUOTE FIX)
							(CADR TEM)))
					      TEM2))
				  (T (PRIN1 TEM2))))))
			  (T (SHOULDNT))))
	       (pop Y)
	       (GO DOCOM))))))

(CHECKPATTERN
  (LAMBDA (FG)                                              (* lmm "20-OCT-78 02:36")
    (AND (OR (NOT (fetch (FILEGROUP DELETED) of FG))
	     (NEQ (LOGAND (JS GTFDB (fetch JFN of FG)
			      1000001Q 4 4)
			  4294967296)
		  0))
	 (OR (EQ (fetch PATTERN of FG)
		 T)
	     (DMATCH (fetch PATTERN of FG)
		     (DUNPACK (JFNS (fetch JFN of FG)
				    150994945
				    (CONSTANT (CONCAT)))
			      CHCONLST2)
		     FG)))))

(FILENAME
  (LAMBDA NIL                                               (* lmm "17-OCT-78 00:41")
    (OR FILENAME (SETQ FILENAME (SETQ FILE (JFNS JFN))))))

(DIR2
  (LAMBDA (NAME FIRSTFLG)                                   (* lmm "17-JAN-79 10:19")

          (* NAME is a file name specification, with *'s and escapes; FIRSTFLG is 0 if this is the first of an OR, and T if 
	  this is the first of an AND or a DIF or the only specification -
	  FIRSTFLG=T means J should be set)


    (PROG (D (N (QUOTE *))
	     (E (OR DEFAULTEXT (QUOTE *)))
	     (V (OR DEFAULTVERS (QUOTE *)))
	     NP EP UP OTHERFIELD)
          (COND
	    ((AND (EQ FIRSTFLG T)
		  (SETQ J (GTJFN NAME (OR DEFAULTEXT (AND (NEQ NAME (QUOTE *))
							  (QUOTE *)))
				 DEFAULTVERS GTJFNFLAGS)))
	      (RESETSAVE NIL (LIST (QUOTE RLJFN)
				   J))
	      (RETURN T)))                                  (* simple GTJFN fails; need to try harder)
          (SETQ UP (UNPACKFILENAME NAME))
          (MAP UP (FUNCTION (LAMBDA (TAIL)
		   (SELECTQ (CAR TAIL)
			    (HOST (ENVAPPLY (QUOTE ERRORX)
					    (LIST (LIST 52Q NAME))
					    (QUOTE DIRECTORY)
					    (QUOTE DIRECTORY)))
			    (DIRECTORY (SETQ D (CADR TAIL))
				       (OR FIRSTFLG (EQ D (MKATOM (JFNS J 10000000000Q SCRATCHSTR)))
					   (ERROR NAME "directory allowed only in first filename")))
			    (NAME (SETQ N (CADR TAIL)))
			    (EXTENSION (SETQ E (CADR TAIL)))
			    (VERSION (SETQ V (CADR TAIL))
				     (OR FIRSTFLG (ERROR NAME 
							 "version allowed only in first filename")))
			    (SETQ OTHERFIELD T))))
	       (FUNCTION CDDR))
          (OR (SETQ NP N)
	      (SETQ NP (SETQ N (QUOTE *))))
          (OR (SETQ EP E)
	      (SETQ EP (SETQ E "")))
          (AND (EQ V (QUOTE *))
	       (SETQ V -3))
          (COND
	    (FIRSTFLG (COND
			((EQ FIRSTFLG T)
			  (COND
			    ((AND (NEQ N (QUOTE *))
				  (STRPOSL (QUOTE (? %%  *))
					   N))
			      (SETQQ N *)))
			  (COND
			    ((AND (NEQ E (QUOTE *))
				  (STRPOSL (QUOTE (? %%  *))
					   E))
			      (SETQQ E *))))
			(T (SETQQ E *)
			   (SETQQ N *)))
		      (OR (SETQ J (COND
			      (OTHERFIELD (GTJFN (PACKFILENAME (NCONC (LIST (QUOTE DIRECTORY)
									    D
									    (QUOTE NAME)
									    N
									    (QUOTE EXTENSION)
									    E
									    (QUOTE VERSION)
									    V)
								      UP))
						 NIL NIL GTJFNFLAGS))
			      (T (LGTJFN D N E V GTJFNFLAGS))))
			  (RETURN))
		      (RESETSAVE NIL (LIST (QUOTE RLJFN)
					   J)))
	    (OTHERFIELD (ERROR NAME "unusual file specification")))
          (COND
	    ((AND (EQ NP (QUOTE *))
		  (EQ EP (QUOTE *)))
	      (RETURN T)))
          (RETURN (NCONC (UNPACK NP)
			 (CONS (QUOTE %.)
			       (UNPACK EP)))))))

(DIR4
  (LAMBDA (FG FIRSTFLG)                                     (* lmm "24-FEB-79 16:55")
    (COND
      ((NLISTP FG)
	(DIR2 (SELECTQ FG
		       ((NIL -)
			 (QUOTE *.*))
		       FG)
	      FIRSTFLG))
      (T (DPAT (CADR FG)
	       (DIR4 (CAR FG)
		     (AND FIRSTFLG (SELECTQ (CADR FG)
					    ((+ OR)
					      0)
					    T)))
	       (DIR4 (CADDR FG)))))))

(DREAD
  (LAMBDA (PROMPT)                                          (* lmm "21-OCT-78 01:28")
    (PROG1 (PROG NIL
	     LP  (PROGN (TAB I 0)
			(PRIN1 PROMPT))
	         (SELECTQ (READC T)
			  ((Y y)
			    (PRIN1 (QUOTE "Yes")
				   T)
			    (RETURN T))
			  ((N n)
			    (PRIN1 (QUOTE "No")
				   T)
			    (RETURN))
			  (? (PRIN1 (QUOTE "Y or N: ")
				    T)
			     (GO LP))
			  (PROGN (PRIN1 "" T)
				 (GO LP))))
	   (add I (NCHARS PROMPT)
		5))))

(DPRIN1
  (LAMBDA (STR)                                             (* lmm "20-OCT-78 02:53")
    (DTAB (NCHARS STR))
    (PRIN1 STR)))

(DTAB
  (LAMBDA (N)                                               (* lmm "20-OCT-78 04:31")
    (TAB I 0)
    (add I N 1)))

(NEXTFILE
  (LAMBDA (FG FLG)                                          (* lmm "21-OCT-78 01:37")
    (PROG (TEM)
          (COND
	    (FLG (GO TEST)))
      LP  (COND
	    ((SETQ TEM (ASSEMBLE NIL
			         (CV (fetch J of FG))
			         (JS GNJFN)
			         (JRST NILRET)
			         (LSH 1 , -23Q)
			         (ADDI 1 , ASZ)
			         (SKIPA)
			     NILRET
			         (MOVE 1 , KNIL)))
	      (COND
		((ZEROP TEM)
		  (add (fetch OLDVER of FG)
		       1))
		(T (replace OLDVER of FG with 0)
		   (COND
		     ((NOT (ZEROP (LOGAND 4 TEM)))
		       (replace NEWDIR of FG with T))))))
	    (T (RETURN)))
      TEST(COND
	    ((CHECKPATTERN FG)
	      (RETURN FG))
	    (T (GO LP))))))

(DIRPATTERN
  (LAMBDA (FG DEFAULTEXT DEFAULTVERS DELETEDFLG)            (* lmm "17-OCT-78 02:18")
    (PROG (J (GTJFNFLAGS (COND
			   (DELETEDFLG 33345)
			   (T 32833))))
          (OR DEFAULTEXT (SETQQ DEFAULTEXT *))
          (OR DEFAULTVERS (SETQ DEFAULTVERS -3))
          (RETURN (NEXTFILE (create FILEGROUP
				    PATTERN _(OR (DIR4 FG T)
						 (RETURN))
				    DELETED _ DELETEDFLG
				    J _ J)
			    T)))))

(DIRPRINTNAME
  (LAMBDA (FLG)                                             (* lmm "20-OCT-78 04:31")
    (COND
      ((NOT NAMEPRINTED)
	(COND
	  ((fetch NEWDIR of FILEGROUP)
	    (TAB 0 T)
	    (TERPRI)
	    (SPACES 3)
	    (PRIN1 (JFNS JFN 1073741825 SCRATCHSTR))
	    (replace NEWDIR of FILEGROUP with NIL)))
	(DTAB 20)
	(PRIN1 (JFNS JFN (COND
		       (FLG 167772161)
		       (T 169885697))
		     SCRATCHSTR))
	(SPACES 1)
	(SETQ NAMEPRINTED T)))))

(DMATCH
  (LAMBDA (PAT LST FG)                                      (* lmm "13-DEC-78 10:54")
    (COND
      ((EQ PAT T)
	T)
      ((NULL PAT)
	(NULL LST))
      (T (SELECTQ (CAR PAT)
		  (OR (OR (DMATCH (CADR PAT)
				  LST)
			  (DMATCH (CDDR PAT)
				  LST)))
		  (AND (AND (DMATCH (CADR PAT)
				    LST FG)
			    (DMATCH (CDDR PAT)
				    LST FG)))
		  (ANDNOT (AND (NOT (DMATCH (CDDR PAT)
					    LST))
			       (DMATCH (CADR PAT)
				       LST FG)))
		  ((%% ?)
		    (AND LST (DMATCH (CDR PAT)
				     (CDR LST))))
		  (( *)
		    (OR (DMATCH (CDR PAT)
				LST)
			(AND LST (DMATCH PAT (CDR LST)))))
		  (COND
		    ((NULL LST)
		      NIL)
		    ((NEQ (CAR PAT)
			  (CAR LST))
		      (COND
			((AND FG (IGREATERP (CHCON1 (CAR LST))
					    (CHCON1 (CAR PAT))))
			                                    (* gone too far)
			  (RLJFN (fetch JFN of FG))
			  NIL))
		      NIL)
		    (T (DMATCH (CDR PAT)
			       (CDR LST)
			       FG))))))))

(DPAT
  (LAMBDA (CONJ X Y TOP)                                    (* lmm "24-FEB-79 16:55")
    (COND
      ((AND (LISTP X)
	    (LISTP Y)
	    (EQ (CAR X)
		(CAR Y)))
	(CONS (CAR X)
	      (DPAT CONJ (CDR X)
		    (CDR Y))))
      (T (AND (OR (NOT TOP)
		  (AND X Y))
	      (CONS (SELECTQ CONJ
			     ((OR +)
			       (QUOTE OR))
			     ((AND *)
			       (QUOTE AND))
			     ((- ANDNOT)
			       (QUOTE ANDNOT))
			     (ERROR CONJ))
		    (CONS X Y)))))))
)
(DEFINEQ

(/UNDELFILE
  (LAMBDA (FILE)                                            (* lmm "27-SEP-78 04:53")
    (PROG NIL
          (UNDOSAVE (LIST (QUOTE /DELFILE)
			  (OR (SETQ FILE (UNDELFILE FILE))
			      (RETURN))))
          (RETURN FILE))))

(/DELFILE
  (LAMBDA (FILE)                                            (* lmm "27-SEP-78 04:53")
    (PROG NIL
          (UNDOSAVE (LIST (QUOTE /UNDELFILE)
			  (OR (SETQ FILE (DELFILE (COND
						    ((SMALLP FILE)
						      (JFNS FILE))
						    (T FILE))))
			      (RETURN))))
          (RETURN FILE))))

(UNDELFILE
  (LAMBDA (FILE)                                            (* lmm "20-OCT-78 02:42")
    (PROG (JFN)
          (COND
	    ((SETQ JFN (GTJFN FILE NIL 0 33345))
	      (RETURN (PROG1 (AND (ASSEMBLE NIL
					    (CQ (VAG JFN))
					    (HRLI 1 , 1)
					    (HRLZI 2 , 40000Q)
					    (MOVEI 3 , 0)
					    (JSYS 64Q)      (* CHFDB)
					    (JUMP 16Q , RNIL)
                                                            (* return nil if fail)
					    (SKIPA 1 , KT)
					RNIL(HRRZ 1 , KNIL))
				  (JFNS JFN))
			     (RLJFN JFN))))))))
)
(NEW/FN (QUOTE DELFILE))
(NEW/FN (QUOTE UNDELFILE))

(RPAQQ DIRCOMMANDS ((- . PAUSE)
		    (AU . AUTHOR)
		    BY COLLECT (COLLECT? PROMPT " ? " COLLECT)
		    COUNTSIZE
		    (DA . WRITEDATE)
		    (DEL . DELETE)
		    (DEL? . DELETE?)
		    DELETE
		    (DELETE? PROMPT " delete? " DELETE)
		    DELETED
		    (LE LENGTH "(" BYTESIZE ")")
		    (OBS . OLDVERSIONS)
		    OLDVERSIONS
		    (OLD OLDERTHAN 90)
		    OLDERTHAN
		    (OU . OUT)
		    OUT P PAUSE (PR . PROTECTION)
		    PROMPT
		    (SI . SIZE)
		    (TI . WRITEDATE)
		    UNDELETE
		    (VERBOSE AUTHOR CREATIONDATE SIZE READDATE WRITEDATE)))

(RPAQQ FILEINFOTYPES ((WRITEDATE 18)
		      (READDATE 18)
		      (CREATIONDATE 18)
		      (LENGTH 9)
		      (BYTESIZE 2)
		      (PROTECTION 6 FIX 6 8)
		      (SIZE 5)
		      (AUTHOR 11)
		      (LENGTH 7)
		      (BYTESIZE 2)))

(ADDTOVAR LISPXMACROS (DIR (DODIR LISPXLINE)))

(ADDTOVAR LISPXCOMS DIR)
(DECLARE: EVAL@COMPILE DONTCOPY 
[DECLARE: EVAL@COMPILE 

(RECORD FILEGROUP (J PATTERN DELETED OLDVER NEWDIR)
		  (ACCESSFNS J (JFN (LOGAND DATUM 262143)))
		  OLDVER _ 0 NEWDIR _ T)
]

(FILESLOAD (SYSLOAD FROM LISPUSERS)
	   CJSYS)

(DECLARE: EVAL@COMPILE 

(PUTPROPS STRPOSL MACRO [X (COND
			     [(AND (EQ (CAR (LISTP (CAR X)))
				       (QUOTE QUOTE))
				   (LISTP (CADAR X)))
			       (LIST (QUOTE STRPOSL)
				     (LIST (QUOTE CONSTANT)
					   (LIST (QUOTE MAKEBITTABLE)
						 (CAR X)
						 (EQ (CADDDR X)
						     T)))
				     (CADR X)
				     (CADDR X)
				     (AND (NEQ (CADDDR X)
					       T)
					  (CADDDR X]
			     (T (QUOTE IGNOREMACRO])
)
)

(ADDTOVAR JSYSES (GNJFN 15 1))
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY
(BLOCK: NIL DIRECTORY DODIR /UNDELFILE /DELFILE UNDELFILE (LOCALVARS . T))
(BLOCK: DODIRECTORY DODIRECTORY CHECKPATTERN DOCOMMANDS DIR2 DIR4 DREAD DPRIN1 DTAB NEXTFILE DIRPATTERN DIRPRINTNAME DMATCH DPAT
	(LOCALFREEVARS COLUMNS VALUE I JFN FILEGROUP NAMEPRINTED SCRATCHSTR GTJFNFLAGS J NAMEFLG FILENAME FILE)
	(SPECVARS DEFAULTVERS DEFAULTEXT)
	(GLOBALVARS DIRCOMMANDS ASKUSERTTBL CHCONLST2 FILEINFOTYPES RESETVARSLST)
	FILENAME)
]



(* Read macros for * and ^W)

(DEFINEQ

(CONTROLW
  [LAMBDA (X Y Z)                                           (* lmm "17-JUN-82 19:36")
    (PROG (TEM)
          [RETURN (COND
		    [(NEQ X T)
		      (TCONC Z (CONSTANT (CHARACTER (CHARCODE ^W]
		    [(NULL Z)                               (* Control-w not inside of a list)
		      (COND
			[(NULL (READLINEP))                 (* and not inside of a readline.
							    read it.)
			  (PRIN1 (QUOTE "^W")
				 T)
			  (TCONC Z (CONSTANT (CHARACTER (CHARCODE ^W]
			((NULL LINE)                        (* trying to delete in empty line.)
			  (PRIN1 (CONSTANT (CHARACTER (CHARCODE BELL)))
				 T)
			  NIL)
			((NULL (CDR LINE))
			  (SETQ TEM (CAR LINE))
			  (SETQ LINE NIL)
			  (GO ECHO))
			((NULL (CDDR LINE))

          (* reason we erase the last two things from LINE and return the first back is so thatif user then types a c.r.
	  the readline will terminate. remember the control-w was typed while in a call to read. Note that if there was just 
	  one thing on the line and the user typed a control-w, a c.r. will not suffice to terminate.)


			  (SETQ TEM (CADR LINE))
			  (SETQ Z (TCONC NIL (CAR LINE)))
			  (SETQ LINE NIL)
			  (GO ECHO))
			(T (SETQ TEM (NLEFT LINE 3))
			   (SETQ TEM (PROG1 (CADDR TEM)
					    (SETQ Z (TCONC NIL (CADR TEM)))
					    (RPLACD TEM)))
			   (GO ECHO]
		    ((NULL (CAR Z))
		      (PRIN1 (CONSTANT (CHARACTER (CHARCODE BELL)))
			     T)
		      Z)
		    (T (SETQ TEM (CADR Z))
		       [COND
			 ((NULL (CDAR Z))
			   (RPLACD Z NIL)
			   (RPLACA Z NIL))
			 ((RPLACD Z (NLEFT (CAR Z)
					   2))
			   (RPLACD (CDR Z]
		       (GO ECHO]
      ECHO(PRIN1 (QUOTE \\)
		 T)
          (PRIN2 [COND
		   ([OR (NLISTP TEM)
			(NULL (CDR TEM))
			(AND (NULL (CDDR TEM))
			     (ATOM (CADR TEM]
		     TEM)
		   (T (CONS (CAR TEM)
			    (QUOTE (--]
		 T)
          (SPACES 1 T)
          (RETURN Z])

(READLINEP
  [LAMBDA (POS)                                             (* wt: "13-JUN-79 15:08")
                                                            (* returns T if under a readline)
    (PROG (SCRATCHPOS)
          (RETURN (AND (SETQ SCRATCHPOS (STKPOS LISPXREADFN -1 POS))
		       (PROG1 (EQ (STKNAME (REALSTKNTH -1 SCRATCHPOS NIL SCRATCHPOS))
				  (QUOTE READLINE))
			      (RELSTK SCRATCHPOS])

(READCOMMENT
  [LAMBDA (FL RDTBL LST)                                    (* lmm " 5-NOV-82 00:36")
    (PROG (X START END NCHARS POS TEM FLG FL1 N)
          [COND
	    ((OR (NULL LST)
		 (CAR LST)
		 (EQ FL T)
		 (EQ NORMALCOMMENTSFLG T)
		 (NOT (RANDACCESSP FL)))                    (* the * appears interior to a lit.)
	      (RETURN (TCONC LST COMMENTFLG]
          (SETQ FLG (ILESSP (SETQ N (POSITION FL))
			    26))

          (* comment starts at left margin. use 26 to take into account font info. (there can be up to three ^F sequences, 
	  which accounts for an extra 12 throwing position off by 12.0 position should be 10 + 1 for left paren + 1 for * + 1 
	  for space.))


          (SETQ POS (GETFILEPTR FL))
          (COND
	    ([SELECTQ (SETQ TEM (RATOM FL RDTBL))
		      ((%( %) %[ %] %" ' %. DECLARATIONS: E)
			T)
		      (COND
			((NUMBERP TEM)
			  T)
			((EQ TEM COMMENTFLG)
			  NIL)
			((AND CLISPFLG CLISPCHARRAY (STRPOSL CLISPCHARRAY TEM))
			  T)
			(T (SELECTQ (PEEKC FL)
				    ((%) %] _)
				      T)
				    NIL]

          (* an attempt to distinguish comments from non comments. if * is followed by list, or only one thing, then assume 
	  not a comment. The numberp check is in case user has done a makefile fast and actually written out a comment in 
	  abbreviated form.)


	      (GO NO)))
          (SETFILEPTR FL (SETQ START (IPLUS POS -2)))
          [COND
	    ((EQ N 2)

          (* problem is that a terpri preceded the *. this can happen for comments that werent prettyprinted, e.g. were part 
	  of data structures. in this case, to get back to the %(, must back up another two characters, 
	  (one for the *, two for c.r./l.f., and one for paren))


	      (SETFILEPTR FL (SETQ START (IPLUS START -2]   (* BACKUP TWO , SKIPPING OVER THE * AND THE "(")
          [COND
	    ((EQ (SKREAD FL)
		 (QUOTE %]))

          (* comment ends in %] means that it is not in a function, and better not use abbreviated form because when recopied 
	  the %] would be recopied, but prettyprint wouldnt know about it and print anther.)


	      (SETFILEPTR FL POS)
	      (RETURN (TCONC LST COMMENTFLG]
          (SETQ NCHARS (IDIFFERENCE (SETQ END (GETFILEPTR FL))
				    START))
          (SETFILEPTR FL (SUB1 END))

          (* READ had previously seen a left-paren before the * read macro was invoked. This backs up over the closing RIGHT 
	  paren so that the read will terminate this list.)


          (COND
	    ([AND NORMALCOMMENTSFLG (OR (AND (BOUNDP NORMALCOMMENTSFLG)
					     (OPENP (SETQ FL1 (EVALV NORMALCOMMENTSFLG))
						    (QUOTE OUTPUT)))
					(OPENP (SETQ FL1 NORMALCOMMENTSFLG)
					       (QUOTE OUTPUT]

          (* if the value of the value of NORMALCOMMENTSFLG, or the value of NORMALCOMMENTSFLG is the name of an open file, 
	  the comment is coied to the file. this provides a mechanism for storing all comments in one file to be associated 
	  with the sysout or makesys. this feature is useful when one is using commentboxes to store text for messages, where 
	  one does not want to have the material in core, and does not want it scattered over many files.
	  the extra levelof indirection is so that the source file can be renamed to a different directory and the variable 
	  simply reset)


	      (SETQ TEM (GETFILEPTR FL1))
	      (COPYBYTES FL FL1 START END)                  (* the copybyes changes the file pointer)
	      (SETFILEPTR FL (SUB1 END))
	      (SETQ START TEM)
	      (SETQ FL NORMALCOMMENTSFLG)))
          [RETURN (LCONC LST (create COMMENTBOX
				     START _ START
				     NCHARS _ NCHARS
				     FLG _ FLG
				     FILE _(OR FL (INPUT]
      NO  (SETFILEPTR FL POS)
          (RETURN (TCONC LST COMMENTFLG])

(GETCOMMENT
  [LAMBDA (X DESTFL DEF)                                    (* DECLARATIONS: UNDOABLE)
                                                            (* lmm " 5-NOV-82 00:32")
    (PROG (ST NC FL FLG STR TEM)
          (RETURN (COND
		    ((AND [OR (NUMBERP (SETQ ST (fetch (COMMENTBOX START) of X)))
			      (AND (LISTP ST)
				   (NUMBERP (CAR ST))
				   (NUMBERP (CDR ST))
				   (SETQ ST (IPLUS (ITIMES (CAR ST)
							   1000)
						   (CDR ST]
			  (NUMBERP (SETQ NC (fetch (COMMENTBOX NCHARS) of X)))
			  (SETQ FL (fetch (COMMENTBOX FILE) of X))
			  (OR (NULL (SETQ FLG (fetch (COMMENTBOX FLG) of X)))
			      (EQ FLG T))
			  (NULL (fetch (COMMENTBOX REST) of X))
			  (NEQ FL DESTFL)
			  (NOT (ILESSP ST 0)))

          (* The NEQ is because if the exact same comment is printed twice, e.g. once in COMS once when printed, then it would
	  already have been updated in this case, we will have to print the actual comment with numbers in it, but when the 
	  file is loaded, the effect will be the same as though we had printed the comment, except that there will be two 
	  comments pointing to the same locations. which is perfectly ok.)


		      [COND
			((BOUNDP FL)                        (* see comment in readcomment)
			  (SETQ FL (EVALV FL]
		      [COND
			((NOT (OPENP FL (QUOTE INPUT)))
			  (COND
			    ([NULL (NLSETQ (SETQ FL (OPENFILE FL (QUOTE INPUT]
			      (COND
				([NOT (MEMB (SETQQ TEM "can't find file ")
					    (LISTGET1 LISPXHIST (QUOTE *LISPXPRINT*]
				  (LISPXPRIN1 TEM T)
				  (LISPXPRIN2 (fetch (COMMENTBOX FILE) of X)
					      T)
				  (LISPXPRIN1 " - comments lost
" T)))
			      (RETURN X)))
			  (RESETSAVE NIL (LIST (QUOTE CLOSEF?)
					       FL]
		      (SETFILEPTR FL ST)
		      (COND
			[(NULL DESTFL)                      (* means read it in. called from GET* edit macro)
			  (RESETVARS ((NORMALCOMMENTSFLG T))
				     (RETURN (/RPLNODE2 X (READ FL FILERDTBL]
			(T [COND
			     (DEF (SETQ STR (CHANGEFONT DEFAULTFONT))
                                                            (* Want to be in defaultfont before we space over.)
				  [COND
				    (FLG (ENDLINE1)
					 (ENDLINE1 10 NIL T))
				    ((IGREATERP (IPLUS (SETQ TEM (POSITION))
						       3)
						FIRSTCOL)   (* This is essentially a TAB, except will cause 
							    changechar to get printed.)
				      (ENDLINE1 FIRSTCOL NIL T))
				    (T (SPACES (IDIFFERENCE FIRSTCOL TEM]
				  (COND
				    (COMMENTFONT (CHANGEFONT COMMENTFONT]
			   (COND
			     ((AND (NEQ NORMALCOMMENTSFLG (QUOTE DONTUPDATE))
				   (NOT (DISPLAYP DESTFL)))

          (* i have several times gotten scrwed by being in a sysout in whcih i had dumped multiple versions of a file and 
	  accidentally deleted the original parent so that the comments were lost and had to be recovered.
	  this updates the comments to point to the newest file. note you can still lose out if you do not edit a particular 
	  function (so that it is simply copied in toto) until after you delete the original parent. of course, you can 
	  recover by doing a loadfns and reediting.)



          (* the NORMALCOMMENTSFLG check is so that prettyprint can disable this when necessary, e.g. when printing COMS.
	  otherwise when it goes to print a coment in a coms, it has already been updated and all that gets put out is the 
	  comment box.)


			       (replace (COMMENTBOX START) of X with (GETFILEPTR DESTFL))
			       (replace (COMMENTBOX FILE) of X with DESTFL)))
			   (COPYBYTES FL DESTFL ST (IPLUS ST NC))
                                                            (* notice we cant do anything bout the presence or 
							    absence of changechar in the magin during the text of 
							    the comment.)
			   (COND
			     (STR (CHANGEFONT STR)))
			   (COND
			     ((AND DEF FLG)
			       (ENDLINE1)
			       (ENDLINE1)))
			   NIL)))
		    (T X])

(PRINTCOMMENT
  [LAMBDA (X)                                               (* lmm " 5-NOV-82 00:34")
    (PROG (FL)
          (RETURN (COND
		    ((OR (NULL DEF)
			 (NULL FORMFLG))                    (* this is not eally a comment and so shouldnt be 
							    prttyprinted as such)
		      (GETCOMMENT X))
		    ((AND (DISPLAYP (SETQ FL (OUTPUT)))
			  **COMMENT**FLG)
		      (PRIN1 **COMMENT**FLG)
		      NIL)
		    ((OR CHANGEFLG0 (DISPLAYP FL))          (* want the comment to actually be prettyprinted in the 
							    case that changeflg0 is on)
		      (GETCOMMENT X))
		    (T                                      (* DEF is bound in prettyprint block)
		       (GETCOMMENT X FL DEF])
)

(RPAQ NORMALCOMMENTSFLG (COND ((NOT (BOUNDP (QUOTE NORMALCOMMENTSFLG)))
			       T)
			      (T NORMALCOMMENTSFLG)))
(PROGN (SETSYNTAX (CHARCODE ^W)
		  (QUOTE (INFIX IMMEDIATE CONTROLW))
		  T)
       (SETSYNTAX (CHARCODE ^W)
		  T EDITRDTBL)
       (ECHOCONTROL (CHARCODE ^W)
		    (QUOTE IGNORE)))
(SETSYNTAX (QUOTE *)
	   (QUOTE (INFIX ALONE NOESC READCOMMENT))
	   FILERDTBL)

(ADDTOVAR PRETTYPRINTMACROS (* . PRINTCOMMENT))

(ADDTOVAR EDITMACROS (GET* NIL (BIND (IF (NEQ (SETQ #1 (GETCOMMENT (##)))
					      (##))
					 ((I : #1)
					  1)
					 NIL))))

(ADDTOVAR EDITCOMSA GET*)
(DECLARE: DOEVAL@COMPILE DONTCOPY 
[DECLARE: EVAL@COMPILE 

(RECORD COMMENTBOX (HEAD START NCHARS FILE FLG . REST)
		   HEAD _ COMMENTFLG)
]
)



(* useful under DIRECTORY)

(DEFINEQ

(SYSOUTP
  [LAMBDA (FILE)                   (* lmm "10-NOV-82 23:28")
    (PROG [(JFN (OR (SMALLP FILE)
		    (GTJFN FILE (QUOTE SAV)
			   0 100001Q]
          (COND
	    ((IGREATERP (GETFILEINFO JFN (QUOTE SIZE))
			113Q)
	      (OR (JS OPENF JFN (SELECTQ (SYSTEMTYPE)
					 (TENEX (XWD 440000Q 202400Q))
					 (XWD 440000Q 200400Q))
		      NIL T)
		  (ERRORX (LIST 11Q FILE)))
                                   (* open file)
	      (RETURN (PROG1 [COND
			       ([IEQP (JS RIN JFN NIL 3 2)
				      (CONSTANT (SIXBIT (QUOTE SYSOUT]
				 ([LAMBDA (N F)
				     (LIST F N]
				   (JS BIN JFN NIL NIL 2)
				   (READASCIZ JFN)))
			       ([IEQP (JS RIN JFN NIL 3 2)
				      (CONSTANT (SIXBIT (QUOTE MAKSYS]
				 (LIST NIL (JS BIN JFN NIL NIL 2)]
			     (JS CLOSF (XWD (COND ((EQ JFN FILE)
						   400000Q)
						  (T 0))
					    JFN))])

(READASCIZ
  [LAMBDA (JFN)                    (* lmm "10-NOV-82 23:28")
                                   (* used by SYSOUTP to read in file name of parent)
    (JS SFBSZ JFN 7)
    (ASSEMBLE NIL
	      (CQ MACSCRATCHSTRING)
	      (FASTCALL UPATM)
	      (CQ JFN)
	      (SUBI 1 , ASZ)
	      (MOVE 2 , 3)
	      (MOVEI 3 , -2 (4))
	      (MOVEI 4 , 0)
	      (JS SIN)
	      (IDPB 2 , 4)
	      (CQ (MKATOM (STRCONC1 (CONSTANT (CONCAT])
)



(* ASSEMBLETRAN)

(DEFINEQ

(ASSEMBLETRAN
  [LAMBDA (FAULTX)                 (* wt: "24-JUL-78 22:08")
                                   (* Compiles the form FAULTX and gives the compiled code as its "translation")
    (COND
      ((AND (NOT DWIMIFYFLG)
	    (GETD (QUOTE COMPILE2)))
	(PROG (LCFIL LAPFLG (STRF T)
		     SVFLG TEM LISPXHIST)
	      (DECLARE (SPECVARS LCFIL LAPFLG STRF SVFLG))
	      (COMPILE2 [SETQ TEM (PACK (LIST [COND
						((LITATOM FAULTFN)
						  FAULTFN)
						(T (SETQ FAULTFN (QUOTE LAMBDA-EXPR]
					      (GENSYM (QUOTE C]
			(COMPILE1A FAULTFN
				   (CONS (QUOTE LAMBDA)
					 (CONS NIL (NCONC1 (PROG (($$LST1 (CDDR EXPR))
								  $$VAL Y $$TEM1 $$TEM2)
							     $$LP[SETQ Y (CAR (OR (LISTP $$LST1)
										  (GO $$OUT]
							         (COND
								   ([NOT (FMEMB (CAR Y)
										(QUOTE (* CLISP: 
											  DECLARE]
								     (GO $$OUT)))
							         (SETQ $$TEM1 Y)
							         [COND
								   [$$TEM2 (FRPLACD $$TEM2
										    (SETQ $$TEM2
										      (LIST $$TEM1]
								   (T (SETQ $$VAL (SETQ $$TEM2
									  (LIST $$TEM1]
							     $$ITERATE
							         (SETQ $$LST1 (CDR $$LST1))
							         (GO $$LP)
							     $$OUT
							         (RETURN $$VAL))
							   FAULTX)))
				   T))
	      (CLISPTRAN FAULTX (LIST TEM))
	      (RETURN FAULTX)))
      (T (ERROR!])
)

(PUTPROPS ASSEMBLE CLISPWORD (ASSEMBLETRAN))
(DEFINEQ

(HELPBINSEARCH
  (LAMBDA (WORD FILE)                                       (* lmm "20-OCT-78 03:07")
                                                            (* used by HELPSYS)
    (PROG ((MIN (IDIFFERENCE (CHCON1 WORD)
			     64))
	   MID MAX A)
          (SETQ FILE (INPUT (INPUT FILE)))
          (SETQ MAX (COND
	      ((ILESSP MIN 1)
		0)
	      ((IGREATERP MIN 26)
		(COND
		  ((AND (IGREATERP MIN 32)
			(ILESSP MIN 59))
		    (IPLUS MIN -32))
		  (T 27)))
	      (T MIN)))
          (SETQ MIN (MPWRD MAX FILE))
          (SETQ MAX (MPWRD (ADD1 MAX)
			   FILE))
      LP  (SETFILEPTR FILE (MPWRD (SETQ MID (IQUOTIENT (IPLUS MIN MAX)
						       2))
				  FILE))
          (COND
	    ((EQUAL (SETQ A (READ FILE))
		    WORD)
	      (RETURN (READ FILE T)))
	    ((ALPHORDER WORD A)
	      (COND
		((IEQP MID MAX)
		  (RETURN))
		(T (SETQ MAX MID))))
	    (T (COND
		 ((IEQP MIN MID)
		   (RETURN))
		 (T (SETQ MIN MID)))))
          (GO LP))))
)
(DECLARE: DOEVAL@COMPILE DONTCOPY 

(PUTPROPS MPWRD MACRO [LAMBDA (N FILE)
			(PROG ((W (MAPWORD (LRSH N 1)
					   FILE)))
			      (RETURN (LOC (COND
					     ((BIT 35 N)
					       (CAR W))
					     (T (CDR W])
)
(PUTPROPS UTILITY COPYRIGHT (NONE))
(DECLARE: DONTCOPY
  (FILEMAP (NIL (2289 16977 (DODIR 2301 . 2974) (DIRECTORY 2978 . 3201) (DODIRECTORY 3205 . 6298) (DOCOMMANDS 6302 . 9111) (
CHECKPATTERN 9115 . 9616) (FILENAME 9620 . 9784) (DIR2 9788 . 12456) (DIR4 12460 . 12868) (DREAD 12872 . 13366) (DPRIN1 13370 . 
13520) (DTAB 13524 . 13662) (NEXTFILE 13666 . 14447) (DIRPATTERN 14451 . 14905) (DIRPRINTNAME 14909 . 15410) (DMATCH 15414 . 16474) 
(DPAT 16478 . 16974)) (16979 18170 (/UNDELFILE 16991 . 17249) (/DELFILE 17253 . 17579) (UNDELFILE 17583 . 18167)) (20427 31629 (
CONTROLW 20439 . 22403) (READLINEP 22407 . 22834) (READCOMMENT 22838 . 26738) (GETCOMMENT 26742 . 30870) (PRINTCOMMENT 30874 . 31626
)) (32456 33834 (SYSOUTP 32468 . 33358) (READASCIZ 33362 . 33831)) (33866 35262 (ASSEMBLETRAN 33878 . 35259)) (35316 36334 (
HELPBINSEARCH 35328 . 36331)))))
STOP
    