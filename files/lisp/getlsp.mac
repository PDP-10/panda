;<JWILSON>LSPGET.MAC;1    14-Feb-77 16:14:09    EDIT BY JWILSON

;Modified Feb 1977 by JWG in parallel with revision of BOOT.MAC :
;777001 is now defined as ptr to the place to patch.

	TITLE	LSPGET
	SUBTTL	J.GOODWIN 8/15/74
	SEARCH	STENEX
;Program to GET a new multi-fork lisp without starting it.
;Patches the regular bootstrap to stop without starting th lisp.
;Gets JFN from tty, then transfers to bootstrap.

START:	HRROI	2,[ASCIZ /<LISP>BOOT.SAV/]
	HRLZI	1,100001
	GTJFN
	 0
	HRLI	1,400000
	GET
	HRRZI	1,400000
	SKIPA	2,.+1
	XWD	1,141
	SEVEC



;NOW PATCH THE BOOTSTRAP
	MOVE	1,[HALTF]
	EXCH	1,@777001	;ABSOLUTE REF. TO MAGIC PATCH PLACE.

	CAMN	1,[JUMPA 141]
	JRST ZOT
	CAME	1,[JRST	141]
	 JRST	[HRROI	1,[ASCIZ /Cannot find patch spot./]
		 PSOUT
		 HALTF]


ZOT:	HRROI	1,[ASCIZ /Name of your losing LISP file: /]
	PSOUT
	HRLZI	1,3
	SKIPA	2,.+1
	XWD	100,101
	GTJFN
	 JRST	ZOT
	JRST	777000
	END	START
