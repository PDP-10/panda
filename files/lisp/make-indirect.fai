;<LISP>LISP.FAI.1, 30-Dec-82 15:02:09, Edit by JQJOHNSON
;created this file.

search monsym

; To build a program that boots LISP, assemble and load this program and
;install the resulting EXE file on the directory where the user expects to
;find LISP.  Before assembling, edit the line FILENM below to contain the
;name of the file where the REAL LISP lives.
;example:
;	@edit lisp.fai
;	. . .
;	@load lisp.fai
;	@save <lisp>lisp

filenm:	asciz/PS:<LISP.JAN83>INTERLISP.SAV/

start:	move 1,[sixbit/LISP/]
	move 2,1
	SETSN
	 jfcl			;no error returns
	movsi 1,(gj%sht+gj%old)
	hrroi 2,filenm
	GTJFN
	 erjmp error
	hrli 1,.fhslf		;get file on top of ourselves
	move 5,[GET]
	move 6,[movei 1,.fhslf]
	move 7,[setz 2,]
	move 8,[SFRKV]		;start ourselves (!) at start address
	jrst 5			;go and do it all

error:	hrroi 1,[asciz/Can't find /]
	PSOUT
	hrroi 1,filenm
	PSOUT
	HALTF
	jrst start
end start
