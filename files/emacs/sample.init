!* -*-Teco-*-!

!* A sample EMACS Init, taken from one Eugene Ciccarelli set up
 * for a user on BBND.!

    !* MKDUMP call should come first, since it prepares the environment
     * (in particular the method of library loading) so that it may be dumped
     * out later, if desired.!

 m(m.mRun Library)MKDUMPPrepare For Loading Libraries

 m(m.mLoad Library)WORDAB	      !* Load word abbrevs library.!
 m(m.mLoad Library)TMACS	      !* Load other neat things.!

	!* Redefine some EMACS keys a bit: !

 1uDisplay Matching Paren	      !* Turn on automatic ).!
				      !* (Except in Teco mode... so we!
				      !* have to do that via a hook.)!
 m.m^R Select Bufferu:.x()	      !* Control-X Control-B to get!
				      !* TMACS version.!
 m.mSend Mailu:.x(M)		      !* Control-X M!
 m.m^R Character Searchu.S	      !* Control-S!
 m.m^R String Searchu..S	      !* Meta-S!
 m.m^R Reverse Character Searchu.R !* Control-R!
 m.m^R Reverse String Searchu..R   !* Meta-R!
 m.m^R Indent Underu..I	      !* Meta-I!
 m.m^R Indent Relativeu...I	      !* Control-Meta-I!

				      !* A few to make better consistency!
				      !* of character/word/list C-/M-/C-M- !

 m.m^R Kill Sexpu...D	      !* Control-Meta-D!
 m.m^R Backward Paragraphu..P      !* Meta-P!
 m.m^R Forward Paragraphu..N	      !* Meta-N!
 m.m^R Backward Up Listu...P	      !* Control-Meta-P!
 m.m^R Down Listu...N	      !* Control-Meta-N!


	!* Set a few modes: !

 m(m.mAuto Fill Mode)		      !* Always fill by default.!
 1,1 m(m.mWord Abbrev Mode)	      !* Allow word abbrev defining.!
 1:< m(m.mRead Word Abbrev File)WORDAB.DEFNS >w
				      !* Read in word abbrevs if any.  Ignore!
				      !* any error, e.g. no file exists.!
 1:< m(m.mRead Incremental Word Abbrev File)INCABS..0 >w
				      !* Read in any word abbrevs that!
				      !* have been defined lately but not!
				      !* yet written out into main abbrev file!
 @:i*| 1:< m(m.mRead Incremental Word Abbrev File)INCABS..0 >w
       |m.vDumped EMACS Startup Hookw	      !* At dump startup time,!
				      !* read in any abbrevs that were!
				      !* defined after dumping out.!

 m(m.mText Mode)		      !* Put initial buffer into Text mode!
 m.mText Modem.vBuffer Creation Hookw	      !* New buffers will!
				      !* automatically come up in Text mode.!
 @:i*|				      !* Define a command to make a wall chart!
    qWord Abbrev Mode(	      !* Save whether we were in it for later!
      0m(m.mWord Abbrev Mode)	      !* Go out of Word Abbrev Mode for chart!
      m(m.mRun Library)ABSTRWall Chart.X	      !* Which will!
				      !* temporarily load ABSTR, make chart!
				      !* (including Control-X commands).!
      )"n			      !* We were in Word Abbrev Mode before.!
	  1,1:m(m.mWord Abbrev Mode)'	      !* So go into it now.!
      			      !* And then return, unloading ABSTR.!
      |m.vMM My Wall Chartw	      !* Call this MM My Wall Chart!


 :i*C Insert wall chart for this EMACS into buffer.
Will temporarily load the ABSTR library and run its MM Wall Chart command,
    which looks at your environment and makes a chart of all the key commands,
    including Control-X commands.
m.vMM ~DOC~ My Wall Chartw	      !* Give the command some documentation.!


 @:i*| 1uDisplay Matching Paren |m.vTeco Mode Hookw
				      !* Want matching ) in Teco mode too.!

	!* Now jump into the normal EMACS init: !

 er<EMACS>EMACS.INIT @y	      !* Read in the init file.!
 etDSK: fsHSNamefsDSNamew	      !* Default to home directory.!
 etTEMP TTY			      !* And to TEMP.TTY.!
 :m(hfx*)			      !* Jump to default EMACS init.!
