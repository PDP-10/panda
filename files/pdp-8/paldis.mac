SUBTTL	WRITTEN BY MARK R. CRISPIN/MRC

VCUS==:1	;CUSTOMER
VDIS==:7	;DISASSEMBLER VERSION
VMIN==:1	;MINOR
VEDT==:2	;EDIT

	LOC	<.JBVER==:137>
	BYTE	(3)VCUS(9)VDIS(6)VMIN(18)VEDT

	DEFINE	TITLE.(VC,VA,VM)<
TITLE	PALDIS -- PDP-10 DISASSEMBLER FOR PDP-8 BINARY FILES VA(VM)-VC
IF2<PRINTX	PALDIS VERSION VA(VM)-VC>
>

	SALL

	TITLE.(\VCUS,\VDIS,\VEDT)	;DEFINE TITLE
	RELOC

	SEARCH	MACSYM,MONSYM
	.REQUI	SYS:MACREL

;USEFUL DEFINITIONS

CH8PCH==200
CH7PCH==100
W==1
S==2
S1==3
A==4
P==17
STAR=="*"
TAB=="	"
SLASH=="/"
ZERO=="0"
CR==15
LF==12

DEFINE OUTSTR (X) <
	PUSH	P,1
	HRROI	1,X
	PSOUT%
	POP	P,1
>

LAST:	0
PTR:	0
DSK:	0

;	***START OF PROGRAM***

PALDIS:	JFCL			;IN CASE OF CCL
	RESET%			;RESET THE WORLD
	SETZ			;CLEAR 0
	MOVE	P,[-20,,PDL-1]	;MOVE OUR PDL TO P
	MOVE	1,[GJ%SHT!GJ%OLD]
	HRROI	2,[ASCIZ/PTR:/]
	GTJFN%
	 JSHLT
	HRRZM	1,PTR
	MOVE	1,[GJ%SHT!GJ%NEW]
	HRROI	2,[ASCIZ/PDP8.DIS/]
	GTJFN%
	 JSHLT
	HRRZM	1,DSK
	MOVE	1,PTR
	MOVE	2,[<FLD 8,OF%BSZ>!OF%RD]
	OPENF%
	 JSHLT
	MOVE	1,DSK
	MOVE	2,[<FLD 7,OF%BSZ>!OF%WR]
	OPENF%
	 JSHLT
	OUTSTR	OKAY		;SAY OKAY SO FAR
DISAWD:	PUSHJ	P,GETC		;GET A WORD
DISAW0:	TRZE	W,CH8PCH	;CHANNEL 8 PUNCH?
	JRST	CH8SER		;YES, HANDLE IT
	TRZE	W,CH7PCH	;CHANNEL 7 PUNCH?
	JRST	CH7SER		;YES, HANDLE IT
	MOVEI	S,(W)		;COPY THE WORD
	PUSHJ	P,GETC		;GET SECOND WORD
	CAILE	W,77		;CHECK FOR VALIDITY
	JRST	TAPERR		;BAD!
	SETO			;SET FLAGS
	LSH	S,6		;SHIFT S OVER SO WE MAY
	ADDI	S,(W)		;ADD SECOND WORD IN
	PUSHJ	P,GETC		;LOOK AT NEXT WORD
	CAIN	W,CH8PCH	;TRAILER?
	JRST	CH8SER		;YES
	PUSHJ	P,OCTOUT	;SEND IT
	MOVEI	W,TAB		;GET A TAB
	PUSHJ	P,PUT		;SEND IT
	MOVEI	W,SLASH		;GET A SLASH
	PUSHJ	P,PUT		;SEND IT
	AOJ	A,		;INCREMENT A
	MOVEI	S,(A)		;GET A
SENACR:	PUSHJ	P,OCTOUT	;SEND IT
	MOVEI	W,CR		;GET A CR
	PUSHJ	P,PUT		;SEND IT
	MOVEI	W,LF		;GET A LF
	PUSHJ	P,PUT		;SEND IT
	MOVE	W,LAST
	JRST	DISAW0		;DISASSEMBLE ANOTHER WORD

;I/O SUBROUTINES

GETC:	PUSH	P,1
	PUSH	P,2
	MOVE	2,W
	MOVE	1,PTR
	BIN%
	 ERJMP	EOF
	MOVEM	2,LAST
	POP	P,2
	POP	P,1
	MOVE	W,LAST
	POPJ	P,		;RETURN

EOF:	OUTSTR	EOFPTR		;NO MORE TO READ
	MOVE	1,PTR
	CLOSF%
	 JSHLT
	MOVE	1,DSK
	CLOSF%
	 JSHLT
BYE:	HALTF%			;EXIT
	JRST	BYE

PUT:	PUSH	P,1
	PUSH	P,2
	MOVE	2,W
	MOVE	1,DSK
	BOUT%
	POP	P,2
	POP	P,1
	POPJ	P,		;RETURN

TAPERR:	OUTSTR	BADTAP		;YELL AT TAPE
	JRST	BYE		;CRY TO MOMMIE
NOINDV:	OUTSTR	BADINP		;YES AT DEVICE
	JRST	BYE		;LEAVE
NOOTDV:	OUTSTR	BADOUT		;YELL AT DEVICE
	JRST	BYE		;LEAVE

;OCTAL OUTPUT SUBROUTINE

OCTOUT:	IDIVI	S,10		;DIVIDE BY 8
	PUSH	P,S1		;SAVE REMAINDER
	SKIPE	S		;FINISHED?
	PUSHJ	P,OCTOUT	;NO, DO IT AGAIN
	POP	P,S		;GET LATEST DIGIT
	MOVEI	W,ZERO(S)	;CONVERT TO ASCII
	PUSHJ	P,PUT		;SEND IT
	POPJ	P,		;RETURN

;SPECIAL CHANNEL PUNCHES SERVICE ROUTINE

CH8SER:	TRZE	W,CH7PCH	;IS CHANNEL 7 ALSO PUNCHED?
	JRST	FLDSER		;SERVICE FIELD REQUEST
	TRZ	W,CH8PCH	;GET RID OF BIT IF STILL THERE
	JUMPN	W,TAPERR	;BAD TAPE IF NOT ZERO
	JUMPE	DISAWD		;IF FLAG=0, AT BEGINNING OF FILE
	OUTSTR	OPTMES		;TYPE OPTION MESSAGE
	INCHRW	W		;GET A CH
	CAIE	W,STAR		;IS IT A STAR?
	JRST	BYE		;NO, LEAVE
	SETZ			;WIPE FLAG
	JRST	DISAWD		;DISASSEMBLE AGAIN
CH7SER:	MOVEI	S,(W)		;COPY W
	LSH	S,6		;SHIFT OVER
	MOVEI	W,TAB		;GET A TAB
	PUSHJ	P,PUT		;SEND IT
	MOVEI	W,STAR		;GET A STAR
	PUSHJ	P,PUT		;SEND IT
	PUSHJ	P,GETC		;GET ANOTHER
	TRZE	W,CH8PCH!CH7PCH	;IS EXTRA STUFF SET?
	JRST	TAPERR		;YES, BAD TAPE
	ADDI	S,(W)		;ADD TWO TOGETHER
	MOVEI	A,-1(S)		;COPY S TO A
	PUSHJ	P,GETC		;GLITCH TO UNGLITCH GLITCH
	JRST	SENACR		;SEND AND CRLF
FLDSER:	MOVEI	A,200		;INITIAL ADDRESS
	MOVEI	S,(W)		;SAVE LOCATION
	MOVEI	W,TAB		;GET TAB
	PUSHJ	P,PUT		;SEND IT
	MOVEI	W,"#"		;TECO THIS TO "FIELD"
	PUSHJ	P,PUT		;SEND IT
	PUSHJ	P,GETC		;UNGLITCH
	LSH	S,-3		;SHIFT OVER
	JRST	SENACR		;SEND IT

OKAY:	ASCIZ/[PROCESSING BINARY TAPE]
/
EOFPTR:	ASCIZ/%OUT OF TAPE ON PTR
/
BADDSK:	ASCIZ/? DISK WRITE ERROR
/
BADTAP:	ASCIZ/? BAD PDP-8 BINARY TAPE
/
BADINP:	ASCIZ/? CAN'T OPEN INPUT DEVICE
/
BADOUT:	ASCIZ/?  CAN'T OPEN OUTPUT DEVICE
/
OPTMES:	ASCIZ/
END OF FILE, TYPE * TO CONTINUE
/

PDL:	BLOCK	20		;LEAVE SOME ROOM

	END	PALDIS
