TITLE	MEMORY EXTENSION CONTROL	VERSION #2	SEPT. 1 '69
SUBTTL	DON WITCRAFT	1-22-66


;MEMORY EXTENSION REGISTERS

INTERNAL IF,IBR,DFF,IFSW,DFSW,IFSAVE,DFSAVE

;MEMORY EXTENSION CONTROL

IFSW:	0			;INSTRUCTION FIELD SWITCHES
IF:	0			;INSTRUCTION FIELD REGISTER
IFSAVE:	0			;SAVE FIELD REGISTER BITS 0-2,IF
IBR:	0			;INSTRUCTION BUFFER REGISTER
DFSW:	0			;DATA FIELD SWITCHES
DFF:	0			;DATA FIELD REGISTER
DFSAVE:	0			;SAVE FIELD REGISTER BITS 3-5,DFF

;MEMORY EXTENSION IOTS

INTERNAL CDF,CIF,RDF,RIF,RIB,RMF


CDF:	SUBI RUNTIM,3
	ANDI MB,70		;CHANGE TO DATA FIELD N
	LSH MB,^D9		;MB6-8=>DFF
	MOVEM MB,DFF
	POPJ PDP,

CIF:	SUBI RUNTIM,3
	ANDI MB,70		;CHANGE INSTRUCTION FIELD
	LSH MB,^D9		;MB6-8=>IB
	MOVEM MB,IBR
	POPJ PDP,

RDF:	SUBI RUNTIM,3
	ANDI AC,7		;READ DATA FIELD
	MOVE AC0,DFF
	LSH AC0,-14		;DFF=>AC6-8
	OR AC,AC0
	POPJ PDP,

RIF:	SUBI RUNTIM,3
	ANDI AC,70		;READ INSTRUCTION FIELD
	MOVE AC0,IF		;IF=>AC6-8
	LSH AC0,-11
	OR AC,AC0
	POPJ PDP,

RIB:	SUBI RUNTIM,3
	ANDI AC,77		;READ INTERRUPT BUFFER
	MOVE AC0,IFSAVE		;SF0-2=>AC6-8
	LSH AC0,-11
	OR AC,AC0
	MOVE AC0,DFSAVE		;SF3-5=>AC9-11
	LSH AC0,-14
	OR AC,AC0
	POPJ PDP,

RMF:	SUBI RUNTIM,3
	MOVE AC0,IFSAVE		;RESTORE MEMORY FIELD
	MOVEM AC0,IBR		;SF0-2=>IB
	MOVE AC0,DFSAVE		;SF3-5=>DFF
	MOVEM AC0,DFF
	POPJ PDP,
	END
