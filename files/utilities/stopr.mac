	TITLE STOPR Start OPR on the CTY
	SUBTTL /MRC

	SEARCH MACSYM,MONSYM
	.REQUIRE SYS:MACREL
	SALL

	STDAC.

STOPR:	RESET%			; reset all I/O
	MOVE P,[IOWD PDLLEN,PDL]
	MOVX T1,.FHSLF		; get our capabilities
	RPCAP%
	IORB T2,T3		; enable all of 'em
	EPCAP%
	HRROI T1,[ASCIZ/OPR:/]	; get OPR's line number
	STDEV%
	 JSHLT
	MOVE T1,T2		; copy device designator
	ASND%			; assign it to pacify CRJOB%'s buggy test
	 JSHLT
	TXO T2,.TTDES		; make TTY designator
	HRRZM T2,CRJBLK+.CJTTY	; store OPR as line number
	MOVX T1,CJ%LOG!CJ%NAM!CJ%FIL!CJ%NPW!CJ%CAP ; start OPR
	MOVX T2,CRJBLK
	CRJOB%			; make the job
	 JSHLT
	HALTF%			; all done
	JRST STOPR

CRJBLK:	-1,,[ASCIZ/OPR/]	; user name
	0			; no password
	0			; no account
	-1,,[ASCIZ/PS:<OPERATOR>OPR.EXE/] ; program to load
	0			; start offset in entry vector
	0			; tty number
	0			; no time limit
	0			; nothing loaded in AC's
	0			; no EXEC flag bits
	0			; no special primary I/O
	0			; no runtime limit
	0			; no capability mask
	0			; no IPCF PID

PDL:	BLOCK <PDLLEN==20>	; stack for errors

	END STOPR
