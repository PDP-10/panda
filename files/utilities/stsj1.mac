	TITLE STSJ1 Start SYSJB1 as detached unlogged in job
	SUBTTL /MRC 10-Jan-87

	SEARCH MACSYM,MONSYM
	.REQUIRE SYS:MACREL
	SALL

	STDAC.

STSJ1:	RESET%			; reset all I/O
	MOVE P,[IOWD PDLLEN,PDL]
	MOVX T1,.FHSLF		; get our capabilities
	RPCAP%
	IORB T2,T3		; enable all of 'em
	EPCAP%
	MOVE T1,[1,,.LOGDE]	; job 0 logging designator 
	GETAB%			; read it from the monitor
	 MOVX T1,.NULIO		; if can't get job 0 tty, use NUL:
	HRLI T1,.NULIO		; should never do primary input
	MOVEM T1,CRJBLK+.CJPRI
	MOVX T1,CJ%LOG!CJ%NAM!CJ%FIL!CJ%NPW!CJ%SPJ!CJ%CAM
	MOVX T2,CRJBLK
	CRJOB%			; make the job
	 JSHLT
	HALTF%			; all done
	JRST STSJ1

CRJBLK:	-1,,[ASCIZ/OPERATOR/]	; OPERATOR
	0			; no password
	0			; no account
	-1,,[ASCIZ/SYSTEM:SYSJB1.EXE/] ; program to load
	0			; start offset in entry vector
	.NULIO			; start job detached
	0			; no time limit
	0			; nothing loaded in AC's
	0			; no EXEC flag bits
	0			; primary I/O
	0			; no runtime limit
	-1			; no capability mask
	0			; no IPCF PID

PDL:	BLOCK <PDLLEN==20>	; stack for errors

	END STSJ1
