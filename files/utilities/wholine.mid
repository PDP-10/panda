TITLE WHOLINE

A=1 ? B=2 ? C=3 ? D=4
H=10 ? M=11 ? S=12 ? TS=13

.DECSAV

JOBBLK:	BLOCK .JIRT+1
USRNAM:	BLOCK 8.
BUFFER:	ASCII/x1jY8 Kp/
	BLOCK 20.

WHOLIN:	RESET%
	MOVEI A,.PRIIN
	GTTYP%
	CAIE B,.TTH19
	 HALTF%
	MOVEI B,.MORSP
	MTOPR%
	HRRZS C
	CAIG C,1200.
	 HALTF%
	GJINF%
	MOVE B,A
	HRROI A,USRNAM
	DIRST%
	 ERJMP LOSE
LOOP:	SETO A,
	MOVE B,[-<.JIRT+1>,,JOBBLK]
	MOVEI C,.JIJNO
	GETJI%
	 ERJMP LOSE
	MOVE A,[170700,,BUFFER+2]
	SETO B,
	MOVE C,[OT%NMN\OT%DAM\OT%SLA\OT%12H]
	ODTIM%
	MOVEI <" >
	IDPB A
	MOVE B,JOBBLK+.JIJNO
	IDIVI B,10.
	JUMPE B,.+3
	ADDI B,"0
	IDPB B,A
	ADDI C,"0
	IDPB C,A
	MOVEI <" >
	IDPB A
	MOVEI "T
	IDPB A
	IDPB A
	MOVEI "Y
	IDPB A
	MOVE B,JOBBLK+.JITNO
	IDIVI B,100
	IDIVI C,10
	JUMPE B,[JUMPN C,TTY1
		 JRST TTY2]
	ADDI B,"0
	IDPB B,A
TTY1:	ADDI C,"0
	IDPB C,A
TTY2:	ADDI D,"0
	IDPB D,A
	MOVEI <" >
	IDPB A
	SKIPA B,[440700,,USRNAM]
	 IDPB A
	ILDB B
	JUMPN .-2
	MOVEI <" >
	IDPB A
	MOVE B,JOBBLK+.JIDNO
	DIRST%
	 ERJMP LOSE
	MOVEI <" >
	IDPB A
	MOVE C,JOBBLK+.JIPNM
SIXLUP:	SETZ B,
	LSHC B,6
	ADDI B,"A-'A
	IDPB B,A
	JUMPN C,SIXLUP
	MOVEI <" >
	IDPB A
	MOVE H,JOBBLK+.JIRT
	IDIV H,[60.*60.*1000.]
	IDIVI M,60.*1000.
	IDIVI S,1000.
	IDIVI TS,100.
	MOVEI C,10.
	MOVEI ":
	SKIPN B,H
	 JRST NOHOUR
	IDIVI B,10.
	ADDI B,"0
	IDPB B,A
	ADDI C,"0
	IDPB C,A
	IDPB A
NOHOUR:	MOVE B,M
	IDIVI B,10.
	ADDI B,"0
	IDPB B,A
	ADDI C,"0
	IDPB C,A
	IDPB A
	MOVE B,S
	IDIVI B,10.
	ADDI B,"0
	IDPB B,A
	ADDI C,"0
	IDPB C,A
	MOVEI ".
	IDPB A
	MOVE B,TS
	ADDI B,"0
	IDPB B,A
	MOVE A
	MOVE A,[14,,.SYSTAT]
	GETAB%
	 ERJMP LOSE
	MOVE B,A
	MOVE A,
	MOVE C,[FL%ONE\FL%PNT\FL%RND\030200]
	FLOUT%
	 ERJMP LOSE
	MOVEI ^[
	IDPB A
	MOVEI "q
	IDPB A
	MOVEI ^[
	IDPB A
	MOVEI "k
	IDPB A
	SETZ
	IDPB A
	MOVEI A,.FHSLF
	RPCAP%
	IOR C,B
	EPCAP%
	SKIPGE A,JOBBLK+.JITNO
	 JRST SLEEPR		; DETACHED?!?
	IORI A,.TTDES
	HRROI B,BUFFER
	TTMSG%
SLEEPR:	MOVEI A,5.*1000.
	DISMS%
	JRST LOOP

LOSE:	HALTF%
	JRST .-1

END WHOLIN
