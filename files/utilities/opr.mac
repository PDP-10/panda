	TITLE OPR JACKET
	SEARCH MACSYM,MONSYM
	STDAC.

START:	RESET%
	MOVX T1,GJ%OLD!GJ%SHT	; try to get TTYINI
	HRROI T2,[ASCIZ/SYS:TTYINI.EXE/]
	GTJFN%
	IFNJE.
	  MOVEM T1,TTIJFN	; save TTYINI JFN
	  MOVX T1,CR%CAP	; make an inferior fork
	  CFORK%
	   ERJMP ERROR
	  MOVEM T1,TTIFRH	; remember TTYINI fork handle
	  MOVE T1,TTIJFN	; get JFN
	  HRL T1,TTIFRH		; stuff the fork
	  GET%
	  HRROI T1,[ASCIZ/TTYINI
/]
	  RSCAN%
	   ERJMP ERROR
	  MOVE T1,TTIFRH	; get back fork handle of inferior
	  SETZ T2,		; run it and wait for it to stop
	  SFRKV%
	  WFORK%
	  KFORK%
	ENDIF.
	MOVE T1,[SIXBIT/OPR/]
	MOVE T2,1
	SETSN%
	 ERJMP .+1		;NO ERROR RETURNS
	HRROI T1,[ASCIZ/TAKE SYSTEM:SYSTEM.CMD
/]
	RSCAN%
	 ERJMP ERROR
	MOVX T1,.RSINI
	RSCAN%
	 ERJMP ERROR
	MOVX T1,GJ%SHT!GJ%OLD
	HRROI T2,[ASCIZ/SYS:OPR.EXE/]
	GTJFN%
	 ERJMP ERROR
	HRLI T1,.FHSLF		;GET FILE ON TOP OF OURSELVES
	MOVE 5,[GET%]
	MOVE 6,[MOVEI T1,.FHSLF]
	MOVE 7,[SETZ T2,]
	MOVE 8,[SFRKV%]		;START OURSELVES (!) AT START ADDRESS
	JRST 5			;GO AND DO IT ALL

ERROR:	HRROI T1,[ASCIZ/OPR startup failed!/]
	ESOUT%
	HALTF%
	JRST START

TTIJFN:	BLOCK 1
TTIFRH:	BLOCK 1

END START
