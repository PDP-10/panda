	Title  RPCInfo	-- RPC Information
	Subttl          -- by Mark K. Lottor, December, 1986

.DECSAV

P=17

;insertions
.INSRT	MID:MACROS
.INSRT	MID:SYMBOLS
.INSRT  DSK:UDP
.INSRT  DSK:XDR
.INSRT  DSK:RPC

Define RetSkp
	Jrst Popj1
Termin

PDLen==40
PDL:	Block PDLen

OurPrt:	0
HstBuf:	Block	100
HstNum:	0
RPCBlk:	Block 10
ArgBlk:	Block 10


Start:	RESET%
	Move P,[PDL(-PDLen)]	;init stack
	Movei 1,.RSINI
	RSCAN%
	 Erjmp Die
	Movei 1,.PRIIN
	Hrroi 2,HstBuf
	Movei 3,100*5-1
	Movei 4,.CHLFD		;JCL always ends with a bare LF
	SIN%			;Grab the JCL
	Setz 4,
	Dpb 4,2			;Clobber the LF to null

	Move 2,[440700,,HstBuf]
FndHst:	Ildb 1,2
	Jumpe 1,NoHst
	Caie 1,.CHSPC
	 Jrst FndHst

	Movei 1,.GTHSN
	GTHST%
	 Erjmp [TypeCR "?No such host"
		Jrst Done]
	Movem 3,HstNum

	GJINF%
	Addi 3,1000.
	GTAD%
	Tdz 1,[-1,,777700]
	Add 1,3
	Movem 1,OurPrt

	Call UDP"GetIQ
	 Jrst [TypeCR "?Couldn't get us a UDP queue"
	       Jrst Die]

	Move 1,OurPrt
	Call RPC"Init		;initialize RPC library
	 Jrst [TypeCR "?Can't init RPC"
	       Jrst Done]

	Movei 4,RPCBlk
	Move 1,HstNum
	Movem 1,R%HOST(4)
	Movei 1,111.		;portmapper port
	Movem 1,R%PORT(4)
	Move 1,[100000.]	;portmapper program number
	Movem 1,R%PROG(4)
	Movei 1,2.		;portmapper version
	Movem 1,R%VERS(4)
	Movei 1,4.		;dump mappings function
	Movem 1,R%PROC(4)
	Setz 1,
	Movem 1,R%ARG(4)	;takes no args

	Movei 1,RPCBlk
	Call RPC"RPC
	 Jrst [Push P,1
	       Type "?RPC error #"
	       Pop P,2
	       Movei 1,.PRIOU
	       Movei 3,10.
	       NOUT%
		jfcl
	       Jrst Done]

	TypeCR	"
Program		Version		Protocol	Port"

NxtMap:	GetUns 1,RP		;another mapping?
	Jumpe 1,NoMap		;no, end
	GetUns 2,RP		;get program
	Call PNum
	Type "		"
	GetUns 2,RP		;get version
	Call PNum
	Type "		"
	GetUns 2,RP		;get protocol
	Call PNum
	Type "		"
	GetUns 2,RP		;get port
	Call PNum
	TypeCR ""
	Jrst NxtMap

PNum:	Movei 1,.PRIOU
	Movei 3,10.
	NOUT%
	 jfcl
	Ret

NoMap:	Jrst Done

NoHst:	TypeCR  "Usage:  RPCINFO hostname"
	Jrst Done

;; Miscellany

Popj1:	Aos	(P)
R:	Ret

Done:	Call UDP"RlsIQ
Die:	Haltf%
	Jrst	Die

 	End Start
