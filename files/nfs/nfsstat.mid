	Title  NFSStat	-- NFS statistics
	Subttl          -- by Mark K. Lottor, December, 1986

.DECSAV

P=17

;insertions
.INSRT	MID:MACROS
.INSRT	MID:SYMBOLS
.INSRT  DSK:UDP
.INSRT  DSK:XDR
.INSRT  DSK:RPC

Define RetSkp
	Jrst Popj1
Termin

PDLen==40
PDL:	Block PDLen

OurPrt:	0
HstBuf:	Block	100
HstNum:	0
RPCBlk:	Block 10
ArgBlk:	Block 10

PNames:	00.,,[Asciz "null"]		;0 (not used)
	01.,,[Asciz "getatr"]
	02.,,[Asciz "setatr"]
	04.,,[Asciz "lookup"]
	06.,,[Asciz "read"]
	08.,,[Asciz "write"]
	09.,,[Asciz "create"]
	10.,,[Asciz "delete"]
	11.,,[Asciz "rename"]
	14.,,[Asciz "crdir"]
	15.,,[Asciz "deldir"]
	16.,,[Asciz "readdir"]
	17.,,[Asciz "getsys"]
	99.,,[Asciz "stats"]
	0


Start:	RESET%
	Move P,[PDL(-PDLen)]	;init stack
	Movei 1,.RSINI
	RSCAN%
	 Erjmp Die
	Movei 1,.PRIIN
	Hrroi 2,HstBuf
	Movei 3,100*5-1
	Movei 4,.CHLFD		;JCL always ends with a bare LF
	SIN%			;Grab the JCL
	Setz 4,
	Dpb 4,2			;Clobber the LF to null

	Move 2,[440700,,HstBuf]
FndHst:	Ildb 1,2
	Jumpe 1,NoHst
	Caie 1,.CHSPC
	 Jrst FndHst

	Movei 1,.GTHSN
	GTHST%
	 Erjmp [TypeCR "?No such host"
		Jrst Done]
	Movem 3,HstNum

	GJINF%
	Addi 3,1000.
	GTAD%
	Tdz 1,[-1,,777700]
	Add 1,3
	Movem 1,OurPrt

	Call UDP"GetIQ
	 Jrst [TypeCR "?Couldn't get us a UDP queue"
	       Jrst Die]

	Move 1,OurPrt
	Call RPC"Init		;initialize RPC library
	 Jrst [TypeCR "?Can't init RPC"
	       Jrst Done]

	Movei 4,RPCBlk
	Move 1,HstNum
	Movem 1,R%HOST(4)
	Movei 1,2049.		;nfs server
	Movem 1,R%PORT(4)
	Move 1,[100003.]	;nfs program number
	Movem 1,R%PROG(4)
	Movei 1,2.		;nfs version
	Movem 1,R%VERS(4)
	Movei 1,99.		;dump statistics
	Movem 1,R%PROC(4)
	Setz 1,
	Movem 1,R%ARG(4)	;takes no args

	Movei 1,RPCBlk
	Call RPC"RPC
	 Jrst [Push P,1
	       Type "?RPC error #"
	       Pop P,2
	       Movei 1,.PRIOU
	       Movei 3,10.
	       NOUT%
		jfcl
	       Jrst Done]

	GetUns 1,RP		;get reply code
	Jumpn 1,[TypeCR "NFS server error"
		 Jrst Done]

	TypeCR	"
				   Total	     Average
Procedure	Calls	       CPU      Time       CPU      Time (ms)"

NxtPrc:	GetUns 1,RP		;another proc?
	Jumpe 1,NoProc		;no, end
	Movei 4,PNames
FNam:	Skipn 3,(4)
	 Jrst NoNam
	Hlrz 2,3
	Camn 2,1
	 Jrst GotNam
	Addi 4,1
	Jrst FNam
GotNam:	Hrro 1,3
	PSOUT%			;name
	SkipA
NoNam:	 Call DNum
	Type "		"
	GetUns 2,RP		;get calls
	move 10,2
	Call DNum
	Type "	"
	GetUns 2,RP		;get cpu
	move 11,2
	Call PNum
	GetUns 2,RP		;get time
	move 12,2
	Call PNum
	Move 2,11
	Idiv 2,10		;avg cpu
	Call PNum	
	Move 2,12
	Idiv 2,10		;avg tim
	Call PNum	
	TypeCR ""
	Jrst NxtPrc

DNum:	Movei 1,.PRIOU
	Movei 3,10.
	NOUT%
	 jfcl
	Ret

PNum:	Fltr 2,2
	Fdvr 2,[1000.0]
	Movei 1,.PRIOU
	Move 3,[fl%one\fl%pnt\fl%ovl\field(8.,fl%fst)\field(1,fl%snd)]
	Flout%
	 jfcl
	Ret

NoProc:	Jrst Done

NoHst:	TypeCR  "Usage:  NFSSTAT hostname"
	Jrst Done

;; Miscellany

Popj1:	Aos	(P)
R:	Ret

Done:	Call UDP"RlsIQ
Die:	Haltf%
	Jrst	Die

 	End Start
