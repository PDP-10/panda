;XDR conversion routines

L16==<777774,,0>		;Mask for left 16-bit byte in word
R16==<3,,777760>		;and for right 16-bit byte.
B32==L16\R16			;Full 32-bit byte

;integer
Define GetInt ac,ptr
	ldb ac,[.BP B32,(ptr)]
	tlne ac,020000
	 tlo ac,740000
	addi ptr,1
Termin

Define PutInt ac,ptr
	dpb ac,[.BP B32,(ptr)]
	addi ptr,1
Termin

;unsigned
Define GetUns ac,ptr
	ldb ac,[.BP B32,(ptr)]
	addi ptr,1
Termin

Define PutUns ac,ptr
	dpb ac,[.BP B32,(ptr)]
	addi ptr,1
Termin

;string.  count in ac, address in ac+1.
Define GetStr ac,ptr
	ldb ac,[.BP B32,(ptr)]
	addi ptr,1
	push p,ac
	addi ac,3
	lsh ac,-2
	move ac+1,ptr
	hrli ac+1,441000
	add ptr,ac
	pop p,ac
Termin

Define PutStr ac,ptr
	dpb ac,[.BP B32,(ptr)]
	addi ptr,1
	addi ac,3
	lsh ac,-2
	hrlz ac+1,ac+1
	hrr ac+1,ptr
	add ptr,ac
	blt ac+1,-1(ptr)
Termin


;xdr.  pre-encoded data.
Define GetXdr ac,ptr
	move ac,(ptr)
	addi ptr,1
Termin

Define PutXdr ac,ptr
	movem ac,(ptr)
	addi ptr,1
Termin
